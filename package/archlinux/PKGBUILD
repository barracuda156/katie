# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# https://wiki.archlinux.org/index.php/Arch_package_guidelines

pkgname=katie-git
pkgver=4.14.0.r8227.97fea3078
pkgrel=1
pkgdesc='C++ toolkit derived from the Qt 4.8 framework'
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://bitbucket.org/smil3y/workspace/projects/KATANA'
license=('LGPL' 'BSD')
depends=('libsm' 'libxcursor' 'libxinerama' 'libxrandr' 'fontconfig'
         'icu' 'jansson' 'libcups' 'xdg-utils' 'libdeflate')
makedepends=('cmake' 'git' 'unifdef')
optdepends=('gnu-free-fonts: last resort font')
source=(
    # "git+https://scm.osdn.net/gitroot/kde/Katie.git"
    "git+https://bitbucket.org/smil3y/Katie.git"
    # "git+https://github.com/fluxer/katie.git"
)
sha1sums=('SKIP')
conflicts=('katie')
# options=('debug')

pkgver() {
    cd Katie
    printf "4.14.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../Katie \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKATIE_TOOLS_SUFFIX="-katie"
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    install -vDm644 ../Katie/LICENSE.BSD \
        "${pkgdir}/usr/share/licenses/katie-git/BSD.txt"
} 
